plugins {
    id 'net.minecraftforge.gradle.forge' version 'FG_2.1-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.spongepowered.mixin' version '0.6-SNAPSHOT'
}

version = "1.0.0"
group = "me.night0721.lilase"
archivesBaseName = "Lilase"
sourceCompatibility = 1.8

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
    maven { url "https://repo.sk1er.club/repository/maven-releases/" }
}

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false
    replace '@VERSION@': project.version
    clientRunArgs += ["--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker", "--mixin mixins.night0721.json"]
}

configurations {
    packageLib
    configurations.implementation.extendsFrom(configurations.packageLib)
}

dependencies {
    packageLib("gg.essential:loader-launchwrapper:1.1.3")
    implementation("gg.essential:essential-1.8.9-forge:2581")
    implementation("com.squareup.okhttp3:okhttp:4.10.0")
    implementation("org.json:json:20220924")
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
    shadow "org.json:json:20220924"
    shadow "com.squareup.okhttp3:okhttp:4.10.0"


}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.night0721.refmap.json'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            "FMLCorePlugin": "me.night0721.lilase.mixins.MixinLoader",
            'ForceLoadAsMod': true,
            'MixinConfigs': 'mixins.night0721.json',
            'ModSide': 'CLIENT',
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker',
            'TweakOrder': '0'
    )
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.runtimeClasspath]
}

reobf {
    shadowJar {
        classpath = project.configurations.compileClasspath
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main { output.resourcesDir = java.outputDir }
}

tasks.reobfJar.dependsOn(shadowJar)